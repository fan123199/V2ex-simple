apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'


android {

    compileSdkVersion 28
    defaultConfig {
        applicationId "im.fdx.v2ex"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 31
        versionName "2.5.2"
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    signingConfigs {

            googlePlay {
                Properties prop = getSignConfig()
                if (prop != null) {
                    keyAlias prop['keyAlias']
                    keyPassword prop['keyPassword']
                    storeFile file(prop['storeFile'])
                    storePassword prop['storePassword']
                }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.googlePlay
        }
    }
}



android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        if(variant.buildType == 'debug') {
            outputFileName = "v2ex-${variant.buildType}-${variant.versionName}.apk"
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    //test related, use test when you really need it, or you forget always.
    testImplementation 'junit:junit:4.12'
    //test end

    //kotlin start
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.20"
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
    implementation 'androidx.core:core-ktx:1.0.1'
    //kotlin end

    //google start
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.legacy:legacy-support-v13:1.0.0"
    implementation("androidx.appcompat:appcompat:1.0.0")/* {
        exclude group: 'androidx.browser', module: 'browser'
    }*/
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.browser:browser:1.0.0" //已经被appcompat包含
    implementation "androidx.preference:preference:1.0.0"
    implementation "com.google.android.material:material:1.0.0"

    implementation "android.arch.work:work-runtime-ktx:1.0.0-beta05"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'


    implementation 'androidx.room:room-runtime:2.0.0'
    kapt 'androidx.room:room-compiler:2.0.0'
    //google end

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'
    implementation 'com.elvishew:xlog:1.4.1'
    implementation 'org.jsoup:jsoup:1.11.3'
//    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'me.drakeet.multitype:multitype:3.5.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.esafirm.android-image-picker:imagepicker:1.13.1'
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
}

def getSignConfig() {
    def file = rootProject.file("keystore.properties")
    def prop = new Properties()
    try {
        prop.load(new FileInputStream(file))
    }catch (FileNotFoundException e) {
        e.printStackTrace()
        return null
    }
    return prop
}

apply plugin: 'com.google.gms.google-services'